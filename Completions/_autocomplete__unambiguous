#autoload

# Does not work correctly.
[[ $_comp_tags == history-lines ]] &&
    return 1

# Is not going to be correct.
[[ -v _autocomplete__partial_list ]] &&
    return 1

# Doesn't make sense to show.
(( compstate[nmatches] < 2 )) &&
    return 1

# Nothing to insert.
[[ $IPREFIX$PREFIX$SUFFIX$ISUFFIX == *${compstate[unambiguous]:#?}* ]] &&
    return 1

local -P tag=unambiguous
_tags $tag
_tags ||
    return
_requested $tag ||
    return

typeset -g _autocomplete__unambiguous="$compstate[unambiguous]"

local unambiguous_format=
zstyle -s ':completion:unambiguous' format unambiguous_format ||
  unambiguous_format=$'%{\e[0;2m%}%Bcommon substring:%b %F{0}%K{11}'"%d%f%k"

builtin compadd -J "$tag" -x ${unambiguous_format//\%d/$compstate[unambiguous]}

false
